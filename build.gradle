group 'com.veritomyx'
version '1.0-SNAPSHOT'

apply plugin: 'java-library'
apply plugin: 'signing'
apply plugin: 'maven'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.11'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, javadocJar
}

if (project.hasProperty("ossrhUsername")) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Checksums'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'Classes to support adding/verifying Veritomyx-style checksums to files'
                    url 'http://veritomyx.com'

                    scm {
                        connection 'scm:git:https://github.com/Veritomyx/Checksums-Java.git'
                        developerConnection 'scm:git:git@github.com:Veritomyx/Checksums-Java.git'
                        url 'https://github.com/Veritomyx/Checksums-Java'
                    }

                    licenses {
                        license {
                            name 'BSD 3-Clause'
                            url 'https://opensource.org/licenses/BSD-3-Clause'
                        }
                    }

                    developers {
                        developer {
                            id 'TDI-tenderholt'
                            name 'Adam Tenderholt'
                            email 'adam.tenderholt@veritomyx.com'
                        }
                    }
                }
            }
        }
    }
}